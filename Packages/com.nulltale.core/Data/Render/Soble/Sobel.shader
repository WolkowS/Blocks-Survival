Shader "Unlit/SobelFilter"
{
	Properties 
	{
		_Delta("Line Thickness", Range(0.0005, 0.0050)) = 0.001
        _DeltaDepth("Depth Thickness", Range(0.0005, 0.0050)) = 0.001
		_Sensitive("Sensitive", Float) = 50
        _DepthImpact("Depth", Float) = 1
		[Toggle(RAW_OUTLINE)]_Raw("Outline Only", Float) = 0
		[Toggle(OUTLINE)]_Outline("Outline", Float) = 0
		[Toggle(POSTERIZE)]_Poseterize("Posterize", Float) = 0
        [Toggle(DISTANCE)]_Distance("Distance", Float) = 0
		_PosterizationCount("Count", int) = 8
		_OutlineColor("OutlineColor", Color) = (0, 0, 0 ,1)
	}
	SubShader 
	{
		Tags { "RenderType"="Transparent" "RenderPipeline" = "UniversalPipeline" }
		LOD 200
		
        ZTest Always
        ZWrite Off
        ZClip false
        Cull Off
		
		Pass
		{
            HLSLPROGRAM
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/SurfaceInput.hlsl"
            #include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Color.hlsl"
            
			#pragma multi_compile __ OUTLINE POSTERIZE DISTANCE
			
            #pragma shader_feature RAW_OUTLINE
			#pragma shader_feature OUTLINE
            #pragma shader_feature POSTERIZE
            #pragma shader_feature DISTANCE
            
            TEXTURE2D(_CameraDepthTexture);
            SAMPLER(sampler_CameraDepthTexture);
            
#ifndef RAW_OUTLINE
            TEXTURE2D(_MainTex);
            SAMPLER(sampler_MainTex);
#endif
            float _Delta;
            float _DeltaDepth;
            float _pow;
			float _Sensitive;
			float _DepthImpact;
            int _PosterizationCount;
			float4 _OutlineColor;
            
            struct Attributes
            {
                float4 positionOS       : POSITION;
                float2 uv               : TEXCOORD0;
            };

            struct Varyings
            {
                float2 uv        : TEXCOORD0;
                float4 vertex : SV_POSITION;
                UNITY_VERTEX_OUTPUT_STEREO
            };
            
            float SampleDepth(float2 uv)
            {
#if defined(UNITY_STEREO_INSTANCING_ENABLED) || defined(UNITY_STEREO_MULTIVIEW_ENABLED)
                return SAMPLE_TEXTURE2D_ARRAY(_CameraDepthTexture, sampler_CameraDepthTexture, uv, unity_StereoEyeIndex).r;
#else
                return SAMPLE_DEPTH_TEXTURE(_CameraDepthTexture, sampler_CameraDepthTexture, uv);
#endif
            }
            float sobel(float2 uv) 
            {
#if DISTANCE
                float dist = LinearEyeDepth(SAMPLE_DEPTH_TEXTURE(_CameraDepthTexture, sampler_CameraDepthTexture, uv), _ZBufferParams);
                float impact = 1 + 1 - clamp(dist / _DepthImpact, 0, 1);
                float deltaPlus = _DeltaDepth * ((impact * impact) - 1);
                float2 delta = float2(_Delta + deltaPlus, _Delta + deltaPlus);
#else
                float2 delta = float2(_Delta, _Delta);
#endif
                
                float hr = 0;
                float vt = 0;
                
                hr += SampleDepth(uv + float2(-1.0, -1.0) * delta) *  1.0;
                hr += SampleDepth(uv + float2( 1.0, -1.0) * delta) * -1.0;
                hr += SampleDepth(uv + float2(-1.0,  0.0) * delta) *  2.0;
                hr += SampleDepth(uv + float2( 1.0,  0.0) * delta) * -2.0;
                hr += SampleDepth(uv + float2(-1.0,  1.0) * delta) *  1.0;
                hr += SampleDepth(uv + float2( 1.0,  1.0) * delta) * -1.0;
                
                vt += SampleDepth(uv + float2(-1.0, -1.0) * delta) *  1.0;
                vt += SampleDepth(uv + float2( 0.0, -1.0) * delta) *  2.0;
                vt += SampleDepth(uv + float2( 1.0, -1.0) * delta) *  1.0;
                vt += SampleDepth(uv + float2(-1.0,  1.0) * delta) * -1.0;
                vt += SampleDepth(uv + float2( 0.0,  1.0) * delta) * -2.0;
                vt += SampleDepth(uv + float2( 1.0,  1.0) * delta) * -1.0;
                
                return sqrt(hr * hr + vt * vt);
            }
            
            Varyings vert(Attributes input)
            {
                Varyings output = (Varyings)0;
                UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(output);

                VertexPositionInputs vertexInput = GetVertexPositionInputs(input.positionOS.xyz);
                output.vertex = vertexInput.positionCS;
                output.uv = input.uv;
                
                return output;
            }
            
            half4 frag (Varyings input) : SV_Target 
            {
                UNITY_SETUP_STEREO_EYE_INDEX_POST_VERTEX(input);

#ifdef OUTLINE
                float s = pow(1 - saturate(sobel(input.uv)), _Sensitive);
#else
				float s = 1.0f;
#endif
#ifdef RAW_OUTLINE
                return half4(s.xxx, 1);
#else
                half4 col = SAMPLE_TEXTURE2D(_MainTex, sampler_MainTex, input.uv);
#ifdef POSTERIZE
                col = pow(abs(col), 0.4545);
                float3 c = RgbToHsv(col.xyz);
                c.z = round(c.z * _PosterizationCount) / _PosterizationCount;
                col = float4(HsvToRgb(c), col.a);
                col = pow(abs(col), 2.2);
#endif
                //return lerp(col , _OutlineColor, 1.0f - s);
                return lerp(col, _OutlineColor, 1.0f - s);
#endif
            }
            
			#pragma vertex vert
			#pragma fragment frag
			
			ENDHLSL
		}
	} 
	FallBack "Diffuse"
}
